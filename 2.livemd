<!-- livebook:{"persist_outputs":true} -->

# Day 2: Rock Paper Scissors

```elixir
Mix.install([
  {:benchee, "~> 1.1"},
  {:benchee_html, "~> 1.0"}
])
```

<!-- livebook:{"output":true} -->

```
:ok
```

## Story

The Elves begin to set up camp on the beach. To decide whose tent gets to be closest to the snack storage, a giant [Rock Paper Scissors](https://en.wikipedia.org/wiki/Rock_paper_scissors) tournament is already in progress.

Rock Paper Scissors is a game between two players. Each game contains many rounds; in each round, the players each simultaneously choose one of Rock, Paper, or Scissors using a hand shape. Then, a winner for that round is selected: Rock defeats Scissors, Scissors defeats Paper, and Paper defeats Rock. If both players choose the same shape, the round instead ends in a draw.

Appreciative of your help yesterday, one Elf gives you an encrypted strategy guide (your puzzle input) that they say will be sure to help you win. "The first column is what your opponent is going to play: `A` for Rock, `B` for Paper, and `C` for Scissors. The second column--" Suddenly, the Elf is called away to help with someone's tent.

The second column, you reason, must be what you should play in response: `X` for Rock, `Y` for Paper, and `Z` for Scissors. Winning every time would be suspicious, so the responses must have been carefully chosen.

The winner of the whole tournament is the player with the highest score. Your total score is the sum of your scores for each round. The score for a single round is the score for the shape you selected (1 for Rock, 2 for Paper, and 3 for Scissors) plus the score for the outcome of the round (0 if you lost, 3 if the round was a draw, and 6 if you won).

Since you can't be sure if the Elf is trying to help you or trick you, you should calculate the score you would get if you were to follow the strategy guide.

For example, suppose you were given the following strategy guide:

```
A Y
B X
C Z
```

This strategy guide predicts and recommends the following:

* In the first round, your opponent will choose Rock (`A`), and you should choose Paper (`Y`). This ends in a win for you with a score of 8 (2 because you chose Paper + 6 because you won).
* In the second round, your opponent will choose Paper (`B`), and you should choose Rock (`X`). This ends in a loss for you with a score of 1 (1 + 0).
* The third round is a draw with both players choosing Scissors, giving you a score of 3 + 3 = **6**.

In this example, if you were to follow the strategy guide, you would get a total score of **15** (8 + 1 + 6).

What would your total score be if everything goes exactly according to your strategy guide?

```elixir
defmodule Advent1 do
  def solve() do
    File.stream!("#{File.cwd!()}/input/2.txt")
    |> Stream.map(&String.trim/1)
    |> Stream.map(&String.split/1)
    |> Stream.map(&score_single_round/1)
    |> Enum.sum()
  end

  def score_single_round([other, me]) do
    score_single_round(replace(other), replace(me))
  end

  # A for Rock, B for Paper, and C for Scissors  
  defp replace("A"), do: :r
  defp replace("B"), do: :p
  defp replace("C"), do: :s

  # X for Rock, Y for Paper, and Z for Scissors
  defp replace("X"), do: :r
  defp replace("Y"), do: :p
  defp replace("Z"), do: :s

  # The score for a single round is
  # the score for the shape you selected
  # plus the score for the outcome of the round
  defp score_single_round(other, me) do
    score_shape(me) + score_outcome(other, me)
  end

  # (1 for Rock, 2 for Paper, and 3 for Scissors)
  defp score_shape(:r), do: 1
  defp score_shape(:p), do: 2
  defp score_shape(:s), do: 3

  # (0 if you lost, 3 if the round was a draw, and 6 if you won).
  # (other, me)
  defp score_outcome(:s, :r), do: 6
  defp score_outcome(:p, :s), do: 6
  defp score_outcome(:r, :p), do: 6
  # draws  
  defp score_outcome(same, same), do: 3
  # all remaining cases are losses
  defp score_outcome(_other, _me), do: 0
end

Advent1.solve()
```

<!-- livebook:{"output":true} -->

```
12794
```

## Part Two

The Elf finishes helping with the tent and sneaks back over to you. "Anyway, the second column says how the round needs to end: X means you need to lose, Y means you need to end the round in a draw, and Z means you need to win. Good luck!"

The total score is still calculated in the same way, but now you need to figure out what shape to choose so the round ends as indicated. The example above now goes like this:

* In the first round, your opponent will choose Rock (A), and you need the round to end in a draw (Y), so you also choose Rock. This gives you a score of 1 + 3 = 4.
* In the second round, your opponent will choose Paper (B), and you choose Rock so you lose (X) with a score of 1 + 0 = 1.
* In the third round, you will defeat your opponent's Scissors with Rock for a score of 1 + 6 = 7.

Now that you're correctly decrypting the ultra top secret strategy guide, you would get a total score of **12**.

Following the Elf's instructions for the second column, what would your total score be if everything goes exactly according to your strategy guide?

```elixir
defmodule Advent2 do
  def solve() do
    File.stream!("#{File.cwd!()}/input/2.txt")
    |> Stream.map(&String.trim/1)
    |> Stream.map(&String.split/1)
    |> Stream.map(&score_single_round/1)
    |> Enum.sum()
  end

  def score_single_round([other, me]) do
    score_single_round(replace(other), replace(me))
  end

  # A for Rock, B for Paper, and C for Scissors
  defp replace("A"), do: :r
  defp replace("B"), do: :p
  defp replace("C"), do: :s

  # X means you need to lose
  # Y means you need to end the round in a draw
  # and Z means you need to win
  defp replace("X"), do: :lose
  defp replace("Y"), do: :draw
  defp replace("Z"), do: :win

  # The score for a single round is
  # the score for the shape you selected
  # plus the score for the outcome of the round
  defp score_single_round(other, choose) do
    me = choose(other, choose)
    score_shape(me) + score_outcome(other, me)
  end

  # decide what to choose, depending on
  # what the other person has choosen
  # and what outcome I want
  defp choose(:r, :win), do: :p
  defp choose(:p, :win), do: :s
  defp choose(:s, :win), do: :r
  defp choose(same, :draw), do: same
  defp choose(:r, :lose), do: :s
  defp choose(:p, :lose), do: :r
  defp choose(:s, :lose), do: :p

  # (1 for Rock, 2 for Paper, and 3 for Scissors)
  defp score_shape(:r), do: 1
  defp score_shape(:p), do: 2
  defp score_shape(:s), do: 3

  # (0 if you lost, 3 if the round was a draw, and 6 if you won).
  # (opponent, me)
  defp score_outcome(:s, :r), do: 6
  defp score_outcome(:p, :s), do: 6
  defp score_outcome(:r, :p), do: 6
  # draws  
  defp score_outcome(same, same), do: 3
  # all remaining cases are losses
  defp score_outcome(_other, _me), do: 0
end

Advent2.solve()
```

<!-- livebook:{"output":true} -->

```
14979
```

## Benchmarks

```elixir
Benchee.run(%{
  "Part 1" => &Advent1.solve/0,
  "Part 2" => &Advent2.solve/0
})

:ok
```

<!-- livebook:{"output":true} -->

```
Operating System: Linux
CPU Information: Intel(R) Core(TM) i7-5500U CPU @ 2.40GHz
Number of Available Cores: 4
Available memory: 7.69 GB
Elixir 1.14.0
Erlang 25.1.2

Benchmark suite executing with the following configuration:
warmup: 2 s
time: 5 s
memory time: 0 ns
reduction time: 0 ns
parallel: 1
inputs: none specified
Estimated total run time: 14 s

Benchmarking Part 1 ...
Benchmarking Part 2 ...

Name             ips        average  deviation         median         99th %
Part 2         47.96       20.85 ms     ±6.52%       20.52 ms       26.67 ms
Part 1         47.19       21.19 ms    ±10.22%       20.79 ms       35.12 ms

Comparison: 
Part 2         47.96
Part 1         47.19 - 1.02x slower +0.34 ms
```

<!-- livebook:{"output":true} -->

```
:ok
```
